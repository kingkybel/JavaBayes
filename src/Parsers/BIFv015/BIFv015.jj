/*
 * Definitions for the InterchangeFormat (BIF version 0.15) parser
 * Author: Fabio Cozman
 * <fgcozman@cs.cmu.edu><http://www.cs.cmu.edu/~fgcozman/home.html>
 *
 * The InterchangeFormat parser is free software; you can
 * redistribute it and/or modify it under the terms of the GNU General
 * Public License as published by the Free Software Foundation, provided
 * that this notice and the name of the author appear in all copies.
 * If you're using the software, please notify fgcozman@cs.cmu.edu so
 * that you can receive updates and patches.
 * The InterchangeFormat parser is distributed "as is", in the
 * hope that it will be useful, but WITHOUT ANY WARRANTY; without even the
 * implied warranty of MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.
 * See the GNU General Public License for more details.
 * You should have received a copy of the GNU General Public License
 * along with the BayesianNetworks package. If not, write to the Free
 * Software Foundation, Inc., 675 Mass Ave, Cambridge, MA 02139, USA.
 */

// ==========================================================
// Options for Jack (only non-default options)
// ==========================================================

options
{
  STATIC = false;
}

// ==========================================================
// Java code that is incorporated into the parser
// ==========================================================

PARSER_BEGIN(BIFv015)

package Parsers.BIFv015;

import InterchangeFormat.*;
import java.util.ArrayList;
import java.util.Iterator;

/**
 * Definition of the Interchange Format class and its variables. The IFBayesNet
 * ifbn contains the parsed Bayesian network. 
 * This parser uses the data structures in the JavaBayes core engine (package
 * BayesianNetworks); other implementationsmay use different data structures.
 */
public class BIFv015 extends InterchangeFormat
{
    IFBayesNet ifbn;

    /**
     * Retrieve the Bayes net interface.
     * @return the interface
     */
    public IFBayesNet getBayesNetFromInterchangeFmt()
    {
        return(ifbn);
    }

    /**
     * Method responsible for globbing undefined text in an input file
     * @throws ParseException
     */
    void globUndefinedText() throws ParseException
    {
	Token t;
	while (true)
        {
		t = getToken(1);
		if ((t.kind == 0) ||
		    (t.kind == NETWORK) ||
		    (t.kind == VARIABLE) ||
	            (t.kind == PROBABILITY))
                {
                    break;
                }
		else
                {
                    getNextToken();
                }
	}
    }
}

PARSER_END(BIFv015)

// ==========================================================
// List of tokens
// ==========================================================

SKIP : /* WHITE SPACE */
{
  " "
| "\t"
| "\n"
| "\r"
| "\f"
}

SKIP: /* Characters that can be used to separate words are ignored */
{
  ","
}

SPECIAL_TOKEN : /* COMMENTS */
{
  <SINGLE_LINE_COMMENT: "//" (~["\n","\r"])* ("\n"|"\r"|"\r\n")>
| <FORMAL_COMMENT: "/**" (~["*"])* "*" ("*" | (~["*","/"] (~["*"])* "*"))* "/">
| <MULTI_LINE_COMMENT: "/*" (~["*"])* "*" ("*" | (~["*","/"] (~["*"])* "*"))* "/">
}

TOKEN :  /* Keywords */
{
  < NETWORK: "network" >
| < VARIABLE: "variable" >
| < PROBABILITY: "probability" >
| < PROPERTY: "property" >
| < VARIABLETYPE: "type" >
| < DISCRETE: "discrete" >
| < DEFAULTVALUE: "default" >
| < TABLEVALUES: "table" >
}


TOKEN : /* Definition of a non-negative number */
{
  < NON_NEGATIVE_NUMBER:
      ["1"-"9"] (["0"-"9"])*
      | (["0"-"9"])+ "." (["0"-"9"])* (<EXPONENT>)?
      | "." (["0"-"9"])+ (<EXPONENT>)?
      | (["0"-"9"])+ (<EXPONENT>)?
  >
|
  < #EXPONENT: ["e","E"] (["+","-"])? (["0"-"9"])+ >
}

TOKEN : /* Definition of a string */
{
  < STRING:
      "\""
      (   (~["\"","\\","\n","\r"])
        | ("\\"
            ( ["n","t","b","r","f","\\","'","\""]
            | ["0"-"7"] ( ["0"-"7"] )?
            | ["0"-"3"] ["0"-"7"] ["0"-"7"]
            )
          )
      )*
      "\""
  >
}

// ==========================================================
// THE INTERCHANGE FORMAT GRAMMAR STARTS HERE
// ==========================================================

/**
 * Basic parsing function. First looks for a Network Declaration,
 * then looks for an arbitrary number of VariableDeclaration or
 * ProbabilityDeclaration non-terminals. The objects are
 * in the vectors ifbn.pvs and ifbn.ifProbFuncs.
 */
void CompilationUnit() :
{
    IFProbabilityVariable pv;
    IFProbabilityFunction ifProbFunc;
}
{
	{
            globUndefinedText();
        }
  NetworkDeclaration()
	{
            globUndefinedText();
        }
  ( pv=VariableDeclaration()
	{
            ifbn.add(pv);
            globUndefinedText();
        }
  |
    ifProbFunc=ProbabilityDeclaration()
	{
            ifbn.add(ifProbFunc);
            globUndefinedText();
        }
  )*
  <EOF>
}

/**
 * Detect and initialize the network
 */
void NetworkDeclaration() :
{
    String s;
    ArrayList properties;
}
{
  <NETWORK> s=getString() properties = NetworkContent()
	{
            ifbn = new IFBayesNet(s, properties);
        }
}

/**
 * Fill the network list of properties.
 */
ArrayList NetworkContent() :
{
    ArrayList properties = new ArrayList();
    String s;
}
{
  "{" ( s=Property()
	{
            properties.add(s);
        }
  )* "}"
	{
            return(properties);
        }
}

/**
 * Detect a variable declaration.
 */
IFProbabilityVariable VariableDeclaration() :
{
    String s;
    IFProbabilityVariable pv;
}
{
  <VARIABLE> s=ProbabilityVariableName() pv=VariableContent(s)
	{
            return(pv);
        }
}

/**
 * Fill a variable list of properties.
 */
IFProbabilityVariable VariableContent(String name) :
{
    String s;
    String values[] = null;
    ArrayList properties = new ArrayList();
    IFProbabilityVariable pv = new IFProbabilityVariable();
}
{
  "{" ( s=Property()
	{
            properties.add(s);
        }
    | values=VariableDiscrete()  )*  "}"
	{
            pv.setName(name);
            pv.setProperties(properties);
            pv.setValues(values);
            return(pv);
        }
}

/**
 * Fill a variable type discrete.
 */
String[] VariableDiscrete() :
{
    String values[] = null;
}
{
  <VARIABLETYPE> <DISCRETE>
    "[" <NON_NEGATIVE_NUMBER> "]" "{"
    values=VariableValuesList()
    "}" ";"
	{
            return(values);
        }
}

/**
 * Get the values of a discrete variable.
 */
String[] VariableValuesList() :
{
    int i;
    String value;
    String values[] = null;
    ArrayList v = new ArrayList();
    Iterator e;
}
{
    value=ProbabilityVariableValue()
	{
            v.add(value);
        }
    ( value=ProbabilityVariableValue()
	{
            v.add(value);
        }
    )*
	{
            values = new String[v.size()];
            for (e=v.iterator(), i=0; e.hasNext(); i++)
            {
		values[i] = (String)(e.next());
            }
            return(values);
        }
}

/**
 * Pick a single word as a probability variable value.
 */
String ProbabilityVariableValue() :
{
    String s;
}
{
   s = getString()
	{
            return(s);
        }
}

/**
 * Detect a probability declaration.
 */
IFProbabilityFunction ProbabilityDeclaration() :
{
    String vs[];
    IFProbabilityFunction ifProbFunc = new IFProbabilityFunction();
}
{
  <PROBABILITY> ProbabilityVariablesList(ifProbFunc) ProbabilityContent(ifProbFunc)
	{
            return(ifProbFunc);
        }
}

/**
 * Parse the list of Probability variables.
 */
void ProbabilityVariablesList(IFProbabilityFunction ifProbFunc) :
{
    int i;
    Iterator e;
    String variable_name;
    int cond = -1;
    String vs[];
    ArrayList v_list = new ArrayList();
}
{
   "("
   variable_name=ProbabilityVariableName()
       [ cond=ConditionalMark(v_list) ]
	{
            v_list.add(variable_name);
        }
   ( variable_name=ProbabilityVariableName()
       [ cond=ConditionalMark(v_list) ]
	{
            v_list.add(variable_name);
        }
   )* ")"
	{
            vs = new String[v_list.size()];
            for (e=v_list.iterator(), i=0; e.hasNext(); i++)
            {
		vs[i] = (String)(e.next());
            }
            ifProbFunc.setVariables(vs);
            if (cond == -1)
            {
                cond = 1;
            }
            ifProbFunc.setConditionalIndex(cond);
        }
}

/**
 * Find the conditional mark.
 */
int ConditionalMark(ArrayList v):
{
}
{
    "|"
    {
        return(v.size());
    }
}

/**
 * Pick a single word as a probability variable name.
 */
String ProbabilityVariableName() :
{
    String s;
}
{
   s=getString()
	{
            return(s);
        }
}

/**
 * Fill a Probability list of properties.
 */
void ProbabilityContent(IFProbabilityFunction ifProbFunc) :
{
    String s = null;
    ArrayList properties = new ArrayList();
    IFProbabilityEntry e = null;
    ArrayList entries = new ArrayList();
    ArrayList defs = new ArrayList();
    ArrayList tabs = new ArrayList();
    double def[] = null;
    double tab[] = null;
}
{
  "{" ( s=Property()
	{
            properties.add(s);
        }
      |
      def=ProbabilityDefaultEntry()
        {
            defs.add(def);
        }
      |
      e=ProbabilityEntry()
	{
            entries.add(e);
        }
      |
      tab=ProbabilityTable()
        {
            tabs.add(tab);
        }
      )* "}"
	{
            ifProbFunc.setProperties(properties);
            ifProbFunc.setDefaults(defs);
            ifProbFunc.setEntries(entries);
            ifProbFunc.setTables(tabs);
        }
}

IFProbabilityEntry ProbabilityEntry() :
{
    String s[];
    double d[];
}
{
   s=ProbabilityValuesList() d=FloatingPointList() ";"
	{
            return( new IFProbabilityEntry(s, d) );
        }
}

/**
 * Parse the list of Probability values in an entry.
 */
String[] ProbabilityValuesList() :
{
    int i;
    Iterator e;
    String variable_name;
    String vs[];
    ArrayList v_list = new ArrayList();
}
{
   "("
   variable_name=ProbabilityVariableValue()
	{
            v_list.add(variable_name);
        }
   ( variable_name=ProbabilityVariableValue()
	{
            v_list.add(variable_name);
        }
   )* ")"
	{
            vs = new String[v_list.size()];
            for (e=v_list.iterator(), i=0; e.hasNext(); i++)
            {
		vs[i] = (String)(e.next());
            }
            return(vs);
        }
}

double[] ProbabilityDefaultEntry() :
{
    double d[];
}
{
  <DEFAULTVALUE> d=FloatingPointList() ";"
	{
            return(d);
        }
}

double[] ProbabilityTable() :
{
    double d[];
}
{
  <TABLEVALUES> d=FloatingPointList() ";"
	{
            return(d);
        }
}

// ======================================================
//          Some general purpose non-terminals
// ======================================================

/**
 * Pick a list of non-negative floating numbers.
 */
double[] FloatingPointList() :
{
    int i;
    Double d;
    double ds[];
    ArrayList d_list = new ArrayList();
    Iterator e;
}
{
  d=FloatingPointNumber()
	{
            d_list.add(d);
        }
  ( d=FloatingPointNumber()
	{
            d_list.add(d);
        }
  )*
	{
            ds = new double[d_list.size()];
            for (e=d_list.iterator(), i=0; e.hasNext(); i++)
            {
		d = (Double)(e.next());
		ds[i] = d.doubleValue();
            }
            return(ds);
        }
}

/**
 * Pick a non-negative floating number. This is necessary to allow
 * ignored characters and comments to exist in the middle
 * of a FloatingPointList().
 */
Double FloatingPointNumber() :
{
    Token t;
}
{
  t=<NON_NEGATIVE_NUMBER>
	{
            return( Double.valueOf(t.image) );
        }
}

/**
 * Property definition.
 */
String Property() :
{
    String s;
}
{
  <PROPERTY> s=getString() ";"
	{
            return(s);
        }
}

/**
 * Retrieve String.
 */
String getString() :
{
    Token t;
}
{
    t = <STRING>
        {
            return( (t.image).substring(1,t.image.length()-1) );
        }
}










